plugins {
    id "com.android.library"
    id "org.jetbrains.kotlin.android"
    id 'com.automattic.android.publish-to-s3'
    id "com.google.devtools.ksp"
}

repositories {
    google()
    maven {
        url "https://a8c-libs.s3.amazonaws.com/android"
        content {
            includeGroup "org.wordpress"
            includeGroup "org.wordpress.wellsql"
        }
    }
}

android {
    namespace "com.automattic.android.experimentation"

    compileSdkVersion rootProject.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lint {
        warningsAsErrors true
        lintConfig file("${project.rootDir}/config/lint/lint.xml")
    }
}

kotlin {
    explicitApi()
}

dependencies {
    def okHttp = "4.12.0"
    implementation "com.squareup.okhttp3:okhttp:$okHttp"

    def moshi = "1.15.1"
    implementation "com.squareup.moshi:moshi:$moshi"
    ksp "com.squareup.moshi:moshi-kotlin-codegen:$moshi"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinxCoroutinesVersion"

    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$kotlinxCoroutinesVersion"
    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.assertj:assertj-core:$assertjVersion"
    testImplementation "com.squareup.okhttp3:mockwebserver:$okHttp"
}

dependencyAnalysis {
    issues {
        onUsedTransitiveDependencies {
            // It doesn't seem necessary to directly declare this
            exclude("com.squareup.okio:okio")
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                from components.release

                groupId 'com.automattic.tracks'
                artifactId 'experimentation'
                // version is set by 'publish-to-s3' plugin
            }
        }
    }
}

