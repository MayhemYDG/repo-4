plugins {
    id "com.android.library"
    id "org.jetbrains.kotlin.android"
    id 'com.automattic.android.publish-to-s3'
}

repositories {
    google()
    mavenCentral()
}

dependencies {
    implementation platform("io.sentry:sentry-bom:$sentryBomVersion")
    implementation "io.sentry:sentry-android-core"
    implementation "io.sentry:sentry-okhttp"
    implementation "io.sentry:sentry-android-fragment"
    implementation "io.sentry:sentry-compose-android"
    implementation "androidx.annotation:annotation:$androidxAnnotationVersion"
    implementation "com.squareup.okhttp3:okhttp:$squareupOkhttpVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinxCoroutinesVersion"
    compileOnly 'androidx.compose.ui:ui:1.6.8'

    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.assertj:assertj-core:$assertjVersion"
    testImplementation "org.mockito.kotlin:mockito-kotlin:$mockitoKotlinVersion"
    testImplementation "org.mockito:mockito-inline:$mockitoInlineVersion"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$kotlinxCoroutinesVersion"

}

android {
    namespace "com.automattic.android.tracks.crashlogging"
    android.buildFeatures.buildConfig = true

    compileSdk rootProject.compileSdkVersion
    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        buildConfigField "String", "SENTRY_TEST_PROJECT_DSN", project.properties.get("sentryTestProjectDSN")
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lint {
        warningsAsErrors true
        lintConfig file("${project.rootDir}/config/lint/lint.xml")
        disable 'ObsoleteLintCustomCheck'
    }
}

afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                from components.release

                groupId 'com.automattic.tracks'
                artifactId 'crashlogging'
                // version is set by 'publish-to-s3' plugin
            }
        }
    }
}
